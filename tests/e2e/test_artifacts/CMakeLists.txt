# Copyright 2022 The IREE Authors
#
# Licensed under the Apache License v2.0 with LLVM Exceptions.
# See https://llvm.org/LICENSE.txt for license information.
# SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception

################################################################################
#                                                                              #
# E2E Test Artifacts Suite                                                     #
#                                                                              #
# Includes the generated cmake files to build the e2e test artifacts.          #
# The cmake files "generated_*.cmake" are generated by the python script:      #
# "build_tools/testing/generate_cmake_e2e_test_artifacts_suite.py" and sould   #
# not be edited manually. Please read README.md for the instructions to update #
# the test artifacts.                                                          #
#                                                                              #
################################################################################

if(NOT IREE_BUILD_E2E_TEST_ARTIFACTS)
  return()
endif()

################################################################################
# Defines the required variables.                                              #
################################################################################
iree_package_name(PACKAGE_NAME)
set(ROOT_ARTIFACTS_DIR "${IREE_BINARY_DIR}/e2e_test_artifacts")

include(generated_e2e_test_fetch_models.cmake)

include(generated_e2e_test_iree_artifacts.cmake)

################################################################################
# Exports benchmark config files                                               #
################################################################################
set(_EXECUTION_BENCHMARK_CONFIG_FILE
  "${ROOT_ARTIFACTS_DIR}/execution_benchmark_config.json")
add_custom_command(
  OUTPUT "${_EXECUTION_BENCHMARK_CONFIG_FILE}"
  COMMAND
    "${Python3_EXECUTABLE}" "${IREE_ROOT_DIR}/build_tools/benchmarks/export_benchmark_config.py"
      execution
      --output "${_EXECUTION_BENCHMARK_CONFIG_FILE}"
  DEPENDS
    "${IREE_ROOT_DIR}/build_tools/benchmarks/export_benchmark_config.py"
  COMMENT "Exporting ${_EXECUTION_BENCHMARK_CONFIG_FILE}"
  VERBATIM
)
set(_EXECUTION_BENCHMARK_CONFIG_TARGET_NAME
  "${_PACKAGE_NAME}_iree-execution-benchmark-config")
add_custom_target("${_EXECUTION_BENCHMARK_CONFIG_TARGET_NAME}"
  DEPENDS "${_EXECUTION_BENCHMARK_CONFIG_FILE}"
)
add_dependencies(iree-benchmark-suites
  "${_EXECUTION_BENCHMARK_CONFIG_TARGET_NAME}")

set(_COMPILATION_BENCHMARK_CONFIG_FILE "${ROOT_ARTIFACTS_DIR}/compilation_benchmark_config.json")
add_custom_command(
  OUTPUT "${_COMPILATION_BENCHMARK_CONFIG_FILE}"
  COMMAND
    "${Python3_EXECUTABLE}" "${IREE_ROOT_DIR}/build_tools/benchmarks/export_benchmark_config.py"
      compilation
      --output "${_COMPILATION_BENCHMARK_CONFIG_FILE}"
  DEPENDS
    "${IREE_ROOT_DIR}/build_tools/benchmarks/export_benchmark_config.py"
  COMMENT "Exporting ${_COMPILATION_BENCHMARK_CONFIG_FILE}"
  VERBATIM
)
set(_COMPILATION_BENCHMARK_CONFIG_TARGET_NAME
  "${_PACKAGE_NAME}_iree-compilation-benchmark-config")
add_custom_target("${_COMPILATION_BENCHMARK_CONFIG_TARGET_NAME}"
  DEPENDS "${_COMPILATION_BENCHMARK_CONFIG_FILE}"
)
add_dependencies(iree-e2e-compile-stats-suites
  "${_COMPILATION_BENCHMARK_CONFIG_TARGET_NAME}")
